# C贸digo Ground Station
Este es el codigo actualizado del sat茅lite en arduino hasta la fecha. En 茅l consta todo el programa hasta la versi贸n tres.

# Declaraci贸n de variables
Al principio se declaran los puertos seriales 10 y 11 como sucede en el sat茅lite, el pin de error, el potenci贸metro, el checksum y tambi茅n el Token para poder enviar/recibir.
  ```bash
#include <SoftwareSerial.h>
SoftwareSerial mySerial(10, 11); // RX, TX

int errpin = 2;
char potent = A0;
unsigned long lastReceived = 0;
unsigned long last = 0;
const unsigned long timeout = 8000;
const unsigned long delay_ang = 1500;

bool satHasToken = false;
unsigned long lastTokenSent = 0;
const unsigned long TOKEN_CYCLE = 2500;

int corruptedFromSat = 0;
unsigned long lastStatsReport = 0;
const unsigned long STATS_INTERVAL = 10000;
```

# Checksum
Al igual que en el c贸digo de sat茅lite, en el GroundStation tambi茅n se calcula y se env铆a el cheskum con las funciones _calcChecksum_ y _sendWithChecksum_ adem谩s del _validateMessage_ para ver si el mensaje esta corrupto o no, es lo mismo que en el sat茅lite.
``` bash
String calcChecksum(const String &msg) {
  uint8_t xorSum = 0;
  for (unsigned int i = 0; i < msg.length(); i++) {
    xorSum ^= msg[i];
  }
  String hex = String(xorSum, HEX);
  hex.toUpperCase();
  if (hex.length() == 1) hex = "0" + hex;
  return hex;
}

void sendWithChecksum(const String &msg) {
  String chk = calcChecksum(msg);
  String fullMsg = msg + "*" + chk;
  mySerial.println(fullMsg);
  Serial.println("GS-> " + fullMsg); // AADIDO: debug
}

bool validateMessage(const String &data, String &cleanMsg) {
  int asterisco = data.indexOf('*');
  if (asterisco == -1) return false;
  
  cleanMsg = data.substring(0, asterisco);
  String chkRecv = data.substring(asterisco + 1);
  String chkCalc = calcChecksum(cleanMsg);
  
  return (chkRecv == chkCalc);
}
```

# Protocolo de aplicaci贸n
El protocolo de aplicaci贸n tambi茅n lo hemos aplicado al Ground Station para que el env铆o y procesamiento de datos sea una tarea mas sencilla.

  ```bash
void prot1(String valor) { Serial.println("1:" + valor); }
void prot2(String valor) { Serial.println("2:" + valor); }
void prot3(String valor) { Serial.println("3:" + valor); }
void prot4(String valor) { Serial.println("4:" + valor); }
void prot5(String valor) { Serial.println("5:" + valor); }
void prot6(String valor) { Serial.println("6:" + valor); }
void prot7(String valor) { Serial.println("7:" + valor); }
void prot8(String valor) { Serial.println("8:e"); }

// === PROTOCOLO ORBITAL (reformatea para Python) ===
void prot9(String valor) {
  int sep1 = valor.indexOf(':');
  int sep2 = valor.indexOf(':', sep1 + 1);
  int sep3 = valor.indexOf(':', sep2 + 1);
  
  if (sep1 > 0 && sep2 > 0 && sep3 > 0) {
    String x = valor.substring(sep1 + 1, sep2);
    String y = valor.substring(sep2 + 1, sep3);
    String z = valor.substring(sep3 + 1);
    
    Serial.print("Position: (X: ");
    Serial.print(x);
    Serial.print(" m, Y: ");
    Serial.print(y);
    Serial.print(" m, Z: ");
    Serial.print(z);
    Serial.println(" m)");
  }
}
```

# Inicio del programa
A partir de aqu铆 inicia todas las funciones del GS, todo lo previo son funciones para poder llevar a cabo el funcionamiento.
  ```bash
void setup() {
  Serial.begin(9600);
  mySerial.begin(9600);
  Serial.println("COMM LISTO orbital");
  pinMode(errpin, OUTPUT);
  lastTokenSent = millis();
  lastStatsReport = millis();
  lastReceived = millis(); // AADIDO: evitar timeout inicial
}
```
# Bucle del Ground Station
A partir de ahora todo esto se hace constantemente para en el arduino de la estaci贸n de tierra. Lo primero que hace es la verificaci贸n del Token para poder enviar o no datos, luego hace la verifiacion del checksum y finalmente la opci贸n de determinar el 谩ngulo a partir de un potenci贸metro.
Por otro lado tambi茅n esta el protocolo de aplicacion y el encendido y apagado del pin en caso de error.
```bash
void loop() {
  unsigned long now = millis();

  // Gesti贸n de turnos
  if (!satHasToken && now - lastTokenSent > TOKEN_CYCLE) {
    sendWithChecksum("67:1");
    satHasToken = true;
    lastTokenSent = now;
  }

  // Estad铆sticas cada 10s
  if (now - lastStatsReport > STATS_INTERVAL) {
    if (corruptedFromSat > 0) {
      Serial.println("99:" + String(corruptedFromSat));
      corruptedFromSat = 0;
    }
    lastStatsReport = now;
  }

  // CORREGIDO: Comandos de usuario con checksum
  if (Serial.available()) {
    String command = Serial.readStringUntil('\n');
    command.trim();
    if (command.length() > 0) {
      // Verificar si el comando YA tiene checksum (viene de Python corregido)
      if (command.indexOf('*') != -1) {
        // Ya tiene checksum, enviar directamente
        mySerial.println(command);
        Serial.println("GS-> " + command); // AADIDO: debug
      } else {
        // No tiene checksum, a帽adirlo (compatibilidad con comandos manuales)
        sendWithChecksum(command);
      }
    }
  }

  // ngulo del potenci贸metro con checksum (CORREGIDO: delay aumentado)
  if (now - last > delay_ang) {
    int potval = analogRead(potent);
    int angle = map(potval, 0, 1023, 180, 0);
    sendWithChecksum("5:" + String(angle));
    last = now;
  }
  
  // Recepci贸n con validaci贸n
  if (mySerial.available()) {
    String data = mySerial.readStringUntil('\n');
    data.trim();

    if (data.length() > 0) {
      String cleanMsg;
      
      if (!validateMessage(data, cleanMsg)) {
        Serial.println("SAT-> CORRUPTO: " + data); // AADIDO: debug
        corruptedFromSat++;
        digitalWrite(errpin, HIGH);
        delay(100);
        digitalWrite(errpin, LOW);
        return;
      }
      
      Serial.println("SAT-> OK: " + cleanMsg); // AADIDO: debug
      
      int sepr = cleanMsg.indexOf(':');
      if (sepr > 0) {
        int id = cleanMsg.substring(0, sepr).toInt();
        String valor = cleanMsg.substring(sepr + 1);

        if (id == 67 && valor == "0") {
          satHasToken = false;
          lastTokenSent = now;
          return;
        }

        // Protocolo normal + orbital
        if (id == 1) prot1(valor);
        else if (id == 2) prot2(valor);
        else if (id == 3) prot3(valor);
        else if (id == 4) prot4(valor);
        else if (id == 5) prot5(valor);
        else if (id == 6) prot6(valor);
        else if (id == 7) prot7(valor);
        else if (id == 8) prot8(valor);
        else if (id == 9) prot9(valor);  // ORBITAL

        if (valor.startsWith("e")) {
          digitalWrite(errpin, HIGH);
          delay(500);
          digitalWrite(errpin, LOW);
        }
      }
      lastReceived = now;
    }
  }

  // Timeout
  if (now - lastReceived > timeout) {
    Serial.println("TIMEOUT: sin datos del sat茅lite");
    digitalWrite(errpin, HIGH);
    delay(100);
    digitalWrite(errpin, LOW);
    delay(50);
    lastReceived = now; // AADIDO: evitar spam de timeout
  }
}
```

